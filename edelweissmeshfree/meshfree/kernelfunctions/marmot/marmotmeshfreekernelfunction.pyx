# cython: freethreading_compatible = True
# -*- coding: utf-8 -*-
#  ---------------------------------------------------------------------
#
#  _____    _      _              _
# | ____|__| | ___| |_      _____(_)___ ___
# |  _| / _` |/ _ \ \ \ /\ / / _ \ / __/ __|
# | |__| (_| |  __/ |\ V  V /  __/ \__ \__ \
# |_____\__,_|\___|_| \_/\_/_\___|_|___/___/
# |  \/  | ___  ___| |__  / _|_ __ ___  ___
# | |\/| |/ _ \/ __| '_ \| |_| '__/ _ \/ _ \
# | |  | |  __/\__ \ | | |  _| | |  __/  __/
# |_|  |_|\___||___/_| |_|_| |_|  \___|\___|
#
#
#  Unit of Strength of Materials and Structural Analysis
#  University of Innsbruck,
#
#  Research Group for Computational Mechanics of Materials
#  Institute of Structural Engineering, BOKU University, Vienna
#
#  2023 - today
#
#  Matthias Neuner |  matthias.neuner@boku.ac.at
#  Thomas Mader    |  thomas.mader@bokut.ac.at
#
#  This file is part of EdelweissMeshfree.
#
#  This library is free software; you can redistribute it and/or
#  modify it under the terms of the GNU Lesser General Public
#  License as published by the Free Software Foundation; either
#  version 2.1 of the License, or (at your option) any later version.
#
#  The full text of the license can be found in the file LICENSE.md at
#  the top level directory of EdelweissMeshfree.
#  ---------------------------------------------------------------------


import numpy as np

cimport cython
cimport libcpp.cast
cimport numpy as np

cimport edelweissmeshfree.meshfree.kernelfunctions.marmot.marmotmeshfreekernelfunction


@cython.final # no subclassing -> cpdef with nogil possible
cdef class MarmotMeshfreeKernelFunctionWrapper:
    # cdef classes cannot subclass.
    def __init__(self, node, str kernelType, **kwargs):
        pass

    def __cinit__(self, node, str kernelType, **kwargs):

        self._node = node
        self._center = np.copy(self._node.coordinates)
        cdef double[::1] center = self._center
        self._dimension = self._center.shape[0]

        self._boundingBoxMin = np.zeros(self._dimension)
        self._boundingBoxMax = np.zeros(self._dimension)

        if kernelType == "BSplineBoxed":

            continuityOrder = int ( kwargs.get("continuityOrder", 2) )
            if continuityOrder == 2:
                self._marmotMeshfreeKernelFunction = <MarmotMeshfreeKernelFunction*> (new MarmotMeshfreeKernelFunctionBSpline2ndOrderBoxed( &center[0], self._dimension, kwargs.get("supportRadius", 1.0),))
            elif continuityOrder == 3:
                self._marmotMeshfreeKernelFunction = <MarmotMeshfreeKernelFunction*> (new MarmotMeshfreeKernelFunctionBSpline3rdOrderBoxed( &center[0], self._dimension, kwargs.get("supportRadius", 1.0),))
            else:
                raise ValueError("Unknown continuity order {:d}, supported orders are 2 and 3".format(continuityOrder))
        else:
            raise ValueError("Unknown kernel type {:s}, supported types are 'BSplineBoxed'".format(kernelType))

    def __dealloc__(self):
        del self._marmotMeshfreeKernelFunction

    @property
    def node(self) -> Node:
        return self._node

    @property
    def center(self) -> np.ndarray:
        return self._center

    @property
    def hasBoxSupport(self) -> bool:
        # Every kernel type this wrapper can construct is a boxed B-spline: the kernel is a tensor
        # product of 1D B-splines about the center, each positive exactly on (-supportRadius,
        # supportRadius), so the support is the strict interior of the bounding box reported by
        # getBoundingBox(). Any future non-boxed kernel type must return False here.
        return True

    def moveTo(self, double[::1] coordinates):
        self._marmotMeshfreeKernelFunction.moveTo(&coordinates[0])

    def getBoundingBox(self, ):

        cdef double[::1] boundingBoxMinView = self._boundingBoxMin
        cdef double[::1] boundingBoxMaxView = self._boundingBoxMax

        self._marmotMeshfreeKernelFunction.getBoundingBox(&boundingBoxMinView[0], &boundingBoxMaxView[0])

        return self._boundingBoxMin, self._boundingBoxMax

    def isCoordinateInCurrentSupport(self, double[::1] coords) -> bool:
        cdef int isInside = self._marmotMeshfreeKernelFunction.isInSupport(&coords[0])
        return isInside

    def isAnyCoordinateInSupport(self, double[:, ::1] coords):
        cdef int i
        cdef int n_points = coords.shape[0]

        # We loop entirely in C.
        # &coords[i, 0] gives the pointer to the start of the i-th row.
        for i in range(n_points):
            if self._marmotMeshfreeKernelFunction.isInSupport(&coords[i, 0]):
                return True

        return False

    def computeKernelFunction(self, double[::1] coords) -> double:
        return self._marmotMeshfreeKernelFunction.computeKernelFunction(&coords[0])
